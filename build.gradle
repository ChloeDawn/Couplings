import groovy.json.JsonOutput
import groovy.json.JsonSlurper

buildscript {
  repositories {
    mavenCentral()
    maven {
      name = 'fabricmc.net'
      url = 'https://maven.fabricmc.net'
    }
  }
  dependencies {
    classpath "net.fabricmc:fabric-loom:$loom"
  }
}

apply plugin: 'java'
apply plugin: 'fabric-loom'

archivesBaseName = rootProject.name
group = 'io.github.sleeplessdevelopment'
version = mod
sourceCompatibility = 1.8

minecraft {
  refmapName = 'mixins/couplings/refmap.json'
}

repositories {
  maven {
    name = 'tehnut.info'
    url = 'https://tehnut.info/maven'
  }
  maven {
    name = 'projectlombok.org/edge-releases'
    url = 'https://projectlombok.org/edge-releases'
  }
}

dependencies {
  minecraft "com.mojang:minecraft:$game"
  mappings "net.fabricmc:yarn:$game+$mappings"
  modCompile "net.fabricmc:fabric-loader:$loader"
  implementation "com.google.code.findbugs:jsr305:$findbugs"
  implementation "org.projectlombok:lombok:$lombok"
  annotationProcessor "org.projectlombok:lombok:$lombok"
}

processResources {
  def metadata = ['fabric.mod.json', 'mixins/couplings/mixins.json']
  sourceSets.main.resources.srcDirs.with {
    from(it) {
      include metadata
      expand version: version, refmap: minecraft.refmapName
    }
    from(it) {
      exclude metadata
    }
  }
  doLast {
    fileTree(dir: outputs.files.asPath, include: metadata).each {
      it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    '-Xlint:all',
    '-XprintRounds',
    '-XprintProcessorInfo',
    '-encoding', 'UTF-8',
    '-Xmaxerrs', Integer.MAX_VALUE,
    '-Xmaxwarns', Integer.MAX_VALUE
  ]
}
